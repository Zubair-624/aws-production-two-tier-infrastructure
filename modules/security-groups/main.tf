// ALB SG, EC2 SG, RDS SG
resource "aws_security_group" "alb" {
    name = "${var.project_name}-${var.environment}-alb-sg"
    description = "Security group for Application Load Balancer"
    vpc_id = var.vpc_id

    ingress {
        description = "Allow HTTP from anywhere"
        from_port = 80
        to_port = 80
        protocol = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    ingress {
        description = "Allow HTTPS from internet"
        from_port = 443
        to_port = 443
        protocol = "tcp"
        cidr_blocks = ["0.0.0.0/0"]
    }

    egress {
        description = "Allow all outbound traffic"
        from_port = 0
        to_port = 0
        protocol = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    tags = {
        Name = "${var.project_name}-${var.environment}-alb-sg"
        Environment = var.environment
        Project = var.project_name
        ManagedBy = "terraform"
    }
}

resource "aws_security_group" "ec2" {
    name = "${var.project_name}-${var.environment}-ec2-sg"
    description = "Security group for ec2 instance in Auto Scaling Group"
    vpc_id = var.vpc_id

    ingress {
        description = "Allow HTTP only from ALB"
        from_port = 80
        to_port = 80
        protocol = "tcp"
        security_groups = [aws_security_group.alb.id]
    }

    ingress {
        description = "Allow SSH from admin IP only"
        from_port = 22
        to_port = 22
        protocol = "tcp"
        cidr_blocks = [var.admin_ip]
    }

    egress {
        description = "Allow all outbound traffic"
        from_port = 0
        to_port = 0
        protocol = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    tags = {
        Name = "${var.project_name}-${var.environment}-ec2-sg"
        Environment = var.environment
        Project = var.project_name
        ManagedBy = "terraform"
    }
}

resource "aws_security_group" "rds" {
    name = "${var.project_name}-${var.environment}-rds-sg"
    description = "Security group for RDS database instance"
    vpc_id = var.vpc_id

    ingress {
        description = "Allow MySQL only from EC2 instance"
        from_port = 3306
        to_port = 3306
        protocol = "tcp"
        security_groups = [aws_security_group.ec2.id]
    }

    egress {
        description = "Allow all outbound traffic"
        from_port = 0
        to_port = 0
        protocol = "-1"
        cidr_blocks = ["0.0.0.0/0"]
    }

    tags = {
        Name = "${var.project_name}-${var.environment}-rds-sg"
        Environment = var.environment
        Project = var.project_name
        ManagedBy = "terraform"
    }
}

